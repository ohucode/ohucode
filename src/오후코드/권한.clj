(ns 오후코드.권한
  (:use [미생.기본]
        [오후코드.기본])
  (:require [오후코드.db :as db]
            [오후코드.보안 :as 보안]))

(함수- 거절하기 [_]
  {:status 403 :body "권한이 없습니다."})

(함수- 없다하기 [_]
  {:status 404 :body "찾을 수 없습니다."})

(함수 로그인필수-미들웨어
  "로그인 처리된 요청만 핸들러에 넘기고, 그렇지 않으면 401응답을 준다.
  검증은 별도로 처리해야 하며, 이건 이미 검증된 로그인 정보를 확인한다."
  ([핸들러]
   (로그인필수-미들웨어 핸들러 (상수함수 {:status 401 :body "로그인해 주세요."})))
  ([핸들러 거절하기]
   (fn [요청]
     ((만약 (세션이용자 요청) 핸들러 거절하기) 요청))))

(함수 터전읽는-미들웨어
  "요청한 터전이 있으면 처리, 없으면 404 내보내기.
  터전은 모두에게 (읽기) 공개이므로, 로그인 확인할 필요없다.
  접근한 터전의 주인 정보를 [요청]맵의 [:오후코드 :터전주인]에 연결한다."
  [핸들러 터전명]
  (fn [요청]
    (가정 [터전주인 (select-keys (db/이용자-열람 터전명)
                                 [:아이디 :성명 :생성일시 :거주지역 :url])]
      (만약 (빈? 터전주인)
        (없다하기 요청)
        (핸들러 (assoc-in 요청 [:오후코드 :터전주인] 터전주인))))))

(함수 터전-쓸수있는-아이디?
  "특정 터전에 접근한 이용자가 쓰기 권한이 있는지 확인.
  지금은 주인만 쓸 수 있다."
  [터전명 아이디]
  (= 터전명 아이디))

(함수 터전쓰는-미들웨어
  "터전이 있는지 확인하고, 있다면 권한을 확인한다.
  권한을 확인할 때는 로그인 정보가 필요하다.
  터전은 공개된 정보이므로, 쓸 권한이 없다면 403 응답을 준다."
  [핸들러 터전명]
  (-> (fn [요청]
        (만약 (터전-쓸수있는-아이디? 터전명 (세션이용자-아이디 요청))
          (핸들러 요청)
          (거절하기 요청)))
      (로그인필수-미들웨어 거절하기)
      (터전읽는-미들웨어 터전명)))

(함수 플젝-읽을수있는-아이디?
  "특정 플젝에 접근한 이용자가 읽기 권한이 있는지 확인.
  공개플젝이면 아무나 읽을 수 있고, 비공개면 주인만 읽을 수 있다."
  [터전명 플젝 아이디]
  (or (:공개 플젝) (= 터전명 아이디)))

(함수 플젝-쓸수있는-아이디?
  "특정 플젝에 접근한 이용자가 쓰기 권한이 있는지 확인.
  지금은 터전주인만 쓸 수 있다."
  [터전명 플젝 아이디]
  (= 터전명 아이디))

(함수 플젝읽는-미들웨어
  [핸들러 터전명 플젝명]
  (-> (fn [요청]
        (가정 [플젝 (db/프로젝트-열람 터전명 플젝명)]
          (만약 (플젝-읽을수있는-아이디? 터전명 플젝 (세션이용자-아이디 요청))
            (핸들러 (assoc-in 요청 [:오후코드 :프로젝트] 플젝))
            (없다하기 요청))))
      (터전읽는-미들웨어 터전명)))

(함수 플젝쓰는-미들웨어
  [핸들러 터전명 플젝명]
  (-> (fn [요청]
        (가정 [플젝 (get-in 요청 [:오후코드 :프로젝트])]
          (만약 (플젝-쓸수있는-아이디? 터전명 플젝 (세션이용자-아이디 요청))
            (핸들러 요청)
            (없다하기 요청))))
      (플젝읽는-미들웨어 터전명 플젝명)))
