(ns 오후코드.권한-test
  (:use [clojure.test]
        [오후코드.권한])
  (:require [오후코드.db :as db]))

(deftest 권한-검사
  (let [핸들러   (constantly {:status 200 :body "정상응답"})
        주인요청 {:session {:이용자 {:아이디 "테스트"}}}
        타인요청 {:session {:이용자 {:아이디 "미생"}}}
        손님요청 {:session nil}]

    (testing "로그인 필수 미들웨어"
      (let [로그인핸들러 (로그인필수-미들웨어 핸들러)]
        (are [상태 응답] (= 상태 (:status 응답))
          200 (로그인핸들러 주인요청)
          200 (로그인핸들러 타인요청)
          401 (로그인핸들러 손님요청))))

    (testing "이름공간 읽는 미들웨어"
      (let [주인장 (atom nil)
            공간핸들러 (fn [요청]
                         (reset! 주인장 (get-in 요청 [:앱 :공간주인]))
                         (핸들러))
            있는공간 (공간읽는-미들웨어 공간핸들러 "테스트")
            없는공간 (공간읽는-미들웨어 공간핸들러 "없는공간")]
        (is (= 404 (:status (없는공간 손님요청))))
        (is (nil? @주인장))
        (is (= 200 (:status (있는공간 손님요청))))
        (is (= "테스트" (:아이디 @주인장)))))

    (testing "공간 쓰는 미들웨어"
      (let [있는공간 (공간쓰는-미들웨어 핸들러 "테스트")
            없는공간 (공간쓰는-미들웨어 핸들러 "없는공간")]
        (are [상태 응답] (= 상태 (:status 응답))
          200 (있는공간 주인요청)
          404 (없는공간 주인요청)
          403 (있는공간 손님요청)
          403 (있는공간 타인요청)
          404 (없는공간 손님요청))))

    (let [이름공간 "테스트"
          공개플젝명 "빈프로젝트"
          비공개플젝명 "비공개리포"]

      (testing "플젝에 접근할 수 있는 권한 확인하기"
        (let [공개플젝 (db/프로젝트-열람 이름공간 공개플젝명)
              비공개플젝 (db/프로젝트-열람 이름공간 비공개플젝명)]
          (is (true?  (:공개 공개플젝)))
          (is (false? (:공개 비공개플젝)))
          (is (true?  (플젝-읽을수있는-아이디? 이름공간 공개플젝 nil)))
          (is (true?  (플젝-읽을수있는-아이디? 이름공간 비공개플젝 이름공간)))
          (is (false? (플젝-읽을수있는-아이디? 이름공간 비공개플젝 nil)))
          (is (false?  (플젝-읽을수있는-아이디? 이름공간 비공개플젝 "다른누군가")))))

      (testing "플젝을 읽는 미들웨어"
        (let [공개플젝   (플젝읽는-미들웨어 핸들러 이름공간 공개플젝명)
              비공개플젝 (플젝읽는-미들웨어 핸들러 이름공간 비공개플젝명)]
          (are [상태 응답] (= 상태 (:status 응답))
            200 (공개플젝 손님요청)
            200 (공개플젝 주인요청)
            200 (공개플젝 타인요청)
            404 (비공개플젝 손님요청)
            200 (비공개플젝 주인요청)
            404 (비공개플젝 타인요청))))

      (testing "플젝에 쓰는 미들웨어"
        (let [공개플젝   (플젝쓰는-미들웨어 핸들러 이름공간 공개플젝명)
              비공개플젝 (플젝쓰는-미들웨어 핸들러 이름공간 비공개플젝명)]
          (are [상태 응답] (= 상태 (:status 응답))
            200 (공개플젝 주인요청)
            404 (공개플젝 손님요청)
            404 (공개플젝 타인요청)
            200 (비공개플젝 주인요청)
            404 (비공개플젝 손님요청)
            404 (비공개플젝 타인요청)))))))
